*** Settings ***
Documentation     A global resource file with reusable keywords and variables.
...
...               The system specific keywords created here form our own
...               domain specific language. They utilize keywords provided
...               by the imported Selenium2Library.
Library           Selenium2Library

*** Variables ***
${BROWSER}        Chrome
${DELAY}          0
${DUT}            http://192.168.1.1

*** Keywords ***
Open Browser To Page
  [Arguments]             ${URL}
  [Documentation]         Opens the browser to provided URL
                          Open Browser    ${URL}    ${BROWSER}
                          Maximize Browser Window
                          Set Selenium Speed    ${DELAY}

Open Browser and Login
  [Documentation]         Opens the browser and logs in as admin
                          Open Browser To Page    ${DUT}
                          Login

Open Browser to config page
  [Documentation]         Opens the config page
                          Open Browser To Page    ${DUT}

Login with user pass
  [Arguments]             ${username}     ${password}
  [Documentation]         Tries to login with the credentials provided
  ${status}               Run Keyword and Ignore Error      Element Should Contain
  ...                     tip   You have exceeded three attempts, please try again
                          Run Keyword if          '${status[0]}' == 'PASS'          Sleep   180

                          Input Text              userName              ${username}
                          Input Text              pcPassword            ${password}
                          Click Element           loginBtn

Login
  [Documentation]         Logs in with right credentials
                          Login with user pass    admin                 admin

Logout
  [Documentation]         Logs out
                          Unselect Frame
                          Select Frame            bottomLeftFrame
                          Click Link              Logout

Logout and Close Browser
  [Documentation]         Logs out and closes the browser
                          Logout
                          Close Browser
